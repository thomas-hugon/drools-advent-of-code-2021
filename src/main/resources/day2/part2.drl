import common.*;
import day2.*;
import java.util.List;

rule "create_result"
when
    not SubmarineStatusPart2()
then
    insert(new SubmarineStatusPart2());
end

rule "parse_line"
when
    $input1: InputLine($ln: lineNumber, $val: value, value matches "^(forward|up|down) \\d+$")
then
    var splitted = $val.split(" ");
    insert (new Action($ln, splitted[0], Integer.parseInt(splitted[1])));
end

rule "inc_forward"
when
    $action: Action(name == "forward")
    not Action(lineNumber < $action.lineNumber)
    $result: SubmarineStatusPart2()
then
    $result.forward($action.value());
    update($result);
    delete($action);
end

rule "inc_depth"
when
    $action: Action(name == "down")
    not Action(lineNumber < $action.lineNumber)
    $result: SubmarineStatusPart2()
then
    $result.down($action.value());
    update($result);
    delete($action);
end

rule "dec_depth"
when
    $action: Action(name == "up")
    not Action(lineNumber < $action.lineNumber)
    $result: SubmarineStatusPart2()
then
    $result.up($action.value());
    update($result);
    delete($action);
end

rule "display_result"
when
    not Action()
    $result: SubmarineStatusPart2()
then
    System.err.println("part2="+$result.result());
end